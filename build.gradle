plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.72'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'com.github.tarcv.zandronum'
version '0.4.0'

repositories {
    mavenCentral()
}

kotlin {
    jvm() { // good for debugging, bad for distribution
    }
    mingwX64("windows") {
        binaries {
            executable()
        }
    }
    linuxX64("linux") {
        binaries {
            executable()
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        nativeMain {
            kotlin.srcDir('src/nativeMain/kotlin')
        }
        windowsMain {
            dependsOn nativeMain
        }
        linuxMain {
            dependsOn nativeMain
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation "org.jetbrains.kotlin:kotlin-reflect"
            }
        }
    }
}

task shadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    def target = kotlin.targets.jvm
    from target.compilations.main.output
    def runtimeClasspath = target.compilations.main.runtimeDependencyFiles
    configurations = [runtimeClasspath]
}
